import groovy.swing.SwingBuilder

/*
 *     Find Us: privacy oriented location tracker for your friends and family.
 *     Copyright (C) 2015 Dzmitry Lazerka dlazerka@gmail.com
 *
 *     This program is free software; you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation; either version 2 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License along
 *     with this program; if not, write to the Free Software Foundation, Inc.,
 *     51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
 *
 */
buildscript {
	repositories {
		jcenter()
	}

	dependencies {
	    // https://bintray.com/android/android-tools/com.android.tools.build.gradle
		classpath 'com.android.tools.build:gradle:2.1.2'

		// https://bintray.com/android/android-tools/com.google.gms.google-services/view
		classpath 'com.google.gms:google-services:3.0.0'
	}
}

apply plugin: 'com.android.application'

repositories {
	jcenter()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

//
//task readPasswordFromInput << {
//    def console = System.console()
//
//    ext.keystorePassword = console.readLine('\n\n\n> Enter keyPassword password: ')
//
//    android.signingConfigs.release.storePassword = ext.keystorePassword
//    android.signingConfigs.release.keyPassword = ext.keystorePassword
//}
//
//tasks.whenTaskAdded { task ->
//    if (task.name == 'validateReleaseSigning') {
//        task.dependsOn readPasswordFromInput
//    }
//}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(':android:validateReleaseSigning')) {
        println 'validateReleaseSigning task found, assembling signed APK'

        // If you want to sign builds, you should create ./keystore.properties file in project root.
        def keystorePropertiesFile = rootProject.file("android/keystore.properties")

        if (!keystorePropertiesFile.exists()) {
            throw new InvalidUserDataException("You must create " + keystorePropertiesFile + " to sign builds.")
        }

        // Keystore password is read from file, by key password is read from user.
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))



        if (keystoreProperties['keyPassword'] != null) {
            println 'Found keyPassword in keystore.properties'

        } else if (System.console() != null) {

            sleep 100 // Otherwise the prompt gets overwritten by other gradle output.
            keystoreProperties['keyPassword'] =
                    new String(System.console().readPassword('\nPlease enter signing key password: '))
        } else {
            new SwingBuilder().edt {
                dialog(modal: true, // Otherwise the build will continue running before you closed the dialog
                        title: 'Enter key password', // Dialog title
                        alwaysOnTop: true, // pretty much what the name says
                        resizable: false, // Don't allow the user to resize the dialog
                        locationRelativeTo: null, // Place dialog in center of the screen
                        pack: true, // We need to pack the dialog (so it will take the size of it's children
                        show: true // Let's show it
                ) {
                    vbox { // Put everything below each other
                        label(text: "Please enter signing key password:")
                        input = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            keystoreProperties['keyPassword'] = input.password;
                            dispose(); // Close dialog
                        })
                    }
                }

            }
        }

        if (keystoreProperties['keyPassword'].size() <= 0) {
            throw new InvalidUserDataException("You must enter a password to proceed.")
        }

        android.signingConfigs.release.storeFile = file(keystoreProperties['storeFile'])
        android.signingConfigs.release.storePassword = keystoreProperties['storePassword']
        android.signingConfigs.release.keyAlias = keystoreProperties['keyAlias']
        android.signingConfigs.release.keyPassword = keystoreProperties['keyPassword']
    }
}

android {
	compileSdkVersion 24
	buildToolsVersion '24.0.1'

	dexOptions {
        incremental true
		// Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
		preDexLibraries = preDexEnabled && !isCi
	}

    signingConfigs {
        release {
            // Values will be overwritten above if needed, but must be non-empty otherwise no zipalign happens.
            storeFile file('??')
            storePassword '??'
            keyAlias '??'
            keyPassword '??'
        }
    }

	defaultConfig {
		applicationId 'me.lazerka.mf.android'
		minSdkVersion 21
		targetSdkVersion 24
		versionCode 1
		versionName '0.7'
		multiDexEnabled true

		buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
		buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        // For Java 8.
        jackOptions {
            enabled true
        }

		// testInstrumentationRunner "me.lazerka.mf.android.TestRunner"
	}
	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/NOTICE.txt'
		exclude 'LICENSE.txt'
	}
	sourceSets {
		main {
			manifest.srcFile 'src/main/AndroidManifest.xml'
		}
		debug {
			manifest.srcFile 'src/debug/AndroidManifest.xml'
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    // Cannot put this file into proguard folder as well, because gradle build would fail.
					'project.pro',
					'proguard/jackson.pro',
					'proguard/guava.pro',
					'proguard/rx-java.pro',
					'proguard/square-okhttp.pro',
					'proguard/square-okio.pro'
            buildConfigField 'String', 'BACKEND_ROOT', '"https://lazerka-mf.appspot.com"'

            signingConfig signingConfigs.release
		}
		debug {
			// Then we'd need to get additional OAuth keys, AndroidManifest etc.
			// applicationIdSuffix '.debug'
			minifyEnabled false

            buildConfigField 'String', 'BACKEND_ROOT', '"http://192.168.0.6:8380"'
//            buildConfigField 'String', 'BACKEND_ROOT', '"http://10.0.2.2:8380"'
		}

	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
//	productFlavors {
//		remote {
//            buildConfigField 'String', 'BACKEND_ROOT', '"https://lazerka-mf.appspot.com"'
//		}
//		local {
//            buildConfigField 'String', 'BACKEND_ROOT', '"http://192.168.0.6:8380"'
//		}
//		emulator {
//            buildConfigField 'String', 'BACKEND_ROOT', '"http://10.0.2.2:8380"'
//		}
//	}
    // Verbose APK file name.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    (String) output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}-${variant.versionCode}.apk"))
        }
    }
	lintOptions {
//		abortOnError false
		textReport true
		textOutput 'stdout'
		warning 'UnusedResources'
		warning 'ResourceType'

        // Invalid package reference in library; not included in Android: java.nio.file. Referenced from okio.Okio.
        warning 'InvalidPackage'

        // It suggests to replace hyphens in values/keys.xml.
        disable 'TypographyDashes'
	}
}

dependencies {
	def playServicesVersion = '9.4.0'

	compile project(':api')
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
	compile "com.google.android.gms:play-services-auth:$playServicesVersion"
	compile "com.google.firebase:firebase-messaging:$playServicesVersion"
	compile "com.google.android.gms:play-services-location:$playServicesVersion"
	compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.firebase:firebase-crash:$playServicesVersion"

	compile 'com.android.support:appcompat-v7:24.1.1'
	compile 'com.android.support:recyclerview-v7:24.1.1'
	compile 'com.android.support:multidex:1.0.1'

	compile 'com.squareup.okhttp:okhttp:2.6.0'

	// See ApiRequest on why we don't use Retrofit
//    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
//    compile 'com.squareup.retrofit:converter-jackson:2.0.0-beta2'
//    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'

	compile 'io.reactivex:rxandroid:1.2.1'
	compile 'io.reactivex:rxjava:1.1.8'
	compile 'org.slf4j:slf4j-android:1.7.21'

	testCompile ('org.testng:testng:6.9.10') {
		exclude group: 'com.google.guava'
	}
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'org.mockito:mockito-all:1.10.19'

	androidTestCompile(
			'org.hamcrest:hamcrest-core:1.3',
			'org.hamcrest:hamcrest-integration:1.3',
	)

	//compile project(path: ':backend', configuration: 'android-endpoints')
}

// For some reason this must be at the end: http://stackoverflow.com/questions/34370603
apply plugin: 'com.google.gms.google-services'
